plugins {
    id 'net.neoforged.moddev'
}

neoForge {
    version = rootProject.neo_version

    parchment {
        mappingsVersion = rootProject.parchment_mappings_version
        minecraftVersion = rootProject.parchment_minecraft_version
    }

    runs {
        client {
            client()
            systemProperty 'neoforge.enabledGameTestNamespaces', rootProject.mod_id
        }

        server {
            server()
            programArgument '--nogui'
            systemProperty 'neoforge.enabledGameTestNamespaces', rootProject.mod_id
        }

        gameTestServer {
            type = "gameTestServer"
            systemProperty 'neoforge.enabledGameTestNamespaces', rootProject.mod_id
        }

        data {
            clientData()
            programArguments.addAll '--mod', rootProject.mod_id, '--all',
                    '--output', file('src/generated/resources/').getAbsolutePath(),
                    '--existing', file('src/main/resources/').getAbsolutePath()
        }

        configureEach {
            systemProperty 'forge.logging.markers', 'REGISTRIES'
            logLevel = org.slf4j.event.Level.DEBUG
        }
    }

    mods {
        "${rootProject.mod_id}" {
            sourceSet(sourceSets.main)
            sourceSet(project(':common').sourceSets.main)
        }
    }

    unitTest {
        enable()
        testedMod = mods."${rootProject.mod_id}"
    }
}

dependencies {
    implementation project(':common')
}

// Include common module's resources in neoforge's resource processing pipeline
sourceSets.main.resources.srcDir project(':common').file('src/main/resources')

// Include common module's compiled classes in the neoforge JAR
tasks.named('jar', Jar) {
    from(project(':common').sourceSets.main.output.classesDirs)
}

// Template expansion for neoforge.mods.toml
def gitDescribe = 'git describe --tags --match v* --always'.execute()
gitDescribe.waitFor()
def gitVersion = gitDescribe.exitValue() == 0
        ? gitDescribe.text.trim().replaceFirst(/^v/, '')
        : mod_version

var generateModMetadata = tasks.register("generateModMetadata", ProcessResources) {
    var replaceProperties = [
            minecraft_version      : minecraft_version,
            minecraft_version_range: minecraft_version_range,
            neo_version            : neo_version,
            mod_id                 : mod_id,
            mod_name               : mod_name,
            mod_license            : mod_license,
            mod_version            : gitVersion,
    ]
    inputs.properties replaceProperties
    expand replaceProperties
    from "src/main/templates"
    into "build/generated/sources/modMetadata"
}
sourceSets.main.resources.srcDir generateModMetadata
neoForge.ideSyncTask generateModMetadata
