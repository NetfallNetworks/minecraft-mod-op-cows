plugins {
    id 'net.neoforged.moddev'
}

// Vanilla mode: use NeoForm (deobfuscated Minecraft) instead of full NeoForge.
// This avoids generating NeoForge mod-loader metadata that would conflict with
// the neoforge module's own metadata when packaged into the final JAR.
neoForge {
    neoFormVersion = rootProject.neoform_version

    // Widen protected Mob fields used by combat goals (NeoForge widens these
    // at runtime, but vanilla-mode NeoForm needs an explicit AT at compile time).
    def at = file('src/main/resources/META-INF/accesstransformer.cfg')
    if (at.exists()) {
        accessTransformers.from(at.absolutePath)
    }

    parchment {
        mappingsVersion = rootProject.parchment_mappings_version
        minecraftVersion = rootProject.parchment_minecraft_version
    }
}

dependencies {
    // NeoForge API (DeferredRegister, DeferredItem, etc.) at compile time only.
    // At runtime the neoforge module provides these classes.
    compileOnly "net.neoforged:neoforge:${rootProject.neo_version}"

    testImplementation 'org.junit.jupiter:junit-jupiter:5.11.0'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

test {
    useJUnitPlatform()
}
